pluginManagement {
    repositories {
        mavenLocal()
        maven {
            allowInsecureProtocol true
            url 'http://dl.bintray.com/umsdk/release'
        }
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

//Gradle8 默认开启version_catalogs
//enableFeaturePreview('VERSION_CATALOGS')
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        mavenLocal()
        maven { url 'https://maven.aliyun.com/repository/releases' }
        maven { url 'https://maven.aliyun.com/repository/google' }
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
        maven { url 'https://maven.aliyun.com/repository/public' }
        google()
        mavenCentral()
    }
    versionCatalogs {
        create('androidxLibs'){
            //别名、group、artifact、version
            library('swiperefreshlayout','androidx.swiperefreshlayout','swiperefreshlayout').version('1.2.0-alpha01')
            //调色板
            library('palette','androidx.palette','palette').version('1.0.0')
            //androidx.core:core-ktx:1.9.0
            library('core-ktx','androidx.core','core-ktx').version('1.9.0')
            //androidx.appcompat:appcompat:1.6.1
            library('appcompat','androidx.appcompat','appcompat').version('1.6.1')

            //navigation
            library('navigation-fragment','androidx.navigation','navigation-fragment-ktx').version('2.5.3')
            library('navigation-ui','androidx.navigation','navigation-ui-ktx').version('2.5.3')

            //paging3
            library('ktx-stdlib-common','org.jetbrains.kotlin','kotlin-stdlib-common').version('1.8.0')
            library('paging-runtime', 'androidx.paging', 'paging-runtime').version('3.1.1')

            bundle('androidx',['swiperefreshlayout','palette','core-ktx','appcompat','navigation-fragment','navigation-ui','ktx-stdlib-common','paging-runtime'])


            //retrofit+okhttp3 =网络库依赖
            library('glide','com.github.bumptech.glide','glide').version('4.15.0')
            library('glide-compiler','com.github.bumptech.glide','compiler').version('4.15.0')
            library('glide-transformations','jp.wasabeef','glide-transformations').version('4.0.0')

            library('retrofit', 'com.squareup.retrofit2', 'retrofit').version('2.9.0')
            library('converter-gson', 'com.squareup.retrofit2', 'converter-gson').version('2.9.0')
            library('okhttp', 'com.squareup.okhttp3', 'okhttp').version('4.9.1')
            library('logging', 'com.squareup.okhttp3', 'logging-interceptor').version('4.9.1')
            bundle('http', ['retrofit', 'converter-gson', 'okhttp', 'logging','glide','glide-transformations'])


            //nav-plugin
            library('asm','org.ow2.asm','asm').version('9.2')
            library('asm-tree','org.ow2.asm','asm-tree').version('9.2')
            library('commons-io','commons-io','commons-io').version('2.6')
            library('kotlinpoet','com.squareup','kotlinpoet').version('1.2.0')
            library('agp','com.android.tools.build','gradle').version('7.4.1')
            bundle('nav-plugin',['asm','asm-tree','commons-io','kotlinpoet','agp'])

            //fastjson
            library('fastjson','com.alibaba','fastjson').version('1.2.59')
        }
        create('googleLibs'){
            //com.google.android.material:material:1.8.0
            library('material','com.google.android.material','material').version('1.8.0')
        }
        create('buildsdk'){
            version('compilesdk','33')
            version('minsdk','24')
            version('targetsdk','33')
        }
    }
}

rootProject.name = "Jetpack_android_online"
include ':app'
include ':nav-plugin'
include ':nav-plugin-runtime'
